import java.util.jar.Manifest
import java.util.jar.Attributes

version = '5.11.0'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "cz.kubacki.gradle.plugins:gradle-nbm-plugin:1.12.0"
        gradleApi()
    }
}

task clean(type: Delete){
    delete 'build'
}

subprojects{
    apply plugin: 'java'
    apply plugin: 'cz.kubacki.nbm'

    configurations {
        compile.transitive = false
        compileClasspath.transitive = false
        compileOnly.transitive = false
        providedCompile.transitive = false
    }    
    
    nbm {
        keyStore {
            keyStoreFile = project.file('sign')
            username = 'altsoft'
            password = 'altsoftsign'
        }
    }
    
    repositories {
        maven {
            url 'http://download.osgeo.org/webdav/geotools/'
        }
        mavenCentral()
        mavenLocal()
        maven {
            url 'http://bits.netbeans.org/maven2/'
        }
        maven {
            url 'http://www.datanucleus.org/downloads/maven2/'
        }
    }

    compileJava{
        options.compilerArgs = ['-sourcepath', "${project.projectDir}/src/main/resources"]
    }

    task filterManifest() {
        doFirst {
            println "-project-: ${project}"
            println "-project(':platypus-application')-: ${project(':platypus-application')}"
            if(project != project(':platypus-application')){
                def manifestFile = generateModuleManifest.getGeneratedManifestFile()
                def Manifest manifest = new Manifest()
                def manifestInput = new FileInputStream(manifestFile)
                try{
                    manifest.read(manifestInput);
                }finally{
                    manifestInput.close()
                }
                def mClassPath = manifest.getMainAttributes().remove(new Attributes.Name("Class-Path"))
                def manifestOutput = new FileOutputStream(manifestFile)
                try{
                    manifest.write(manifestOutput);
                }finally{
                    manifestOutput.close();
                }
            }
        }
    }
    filterManifest.dependsOn(generateModuleManifest)
    jar.dependsOn(filterManifest)
    
    task suite(type: Copy){
        from "build/module"
        into "${parent.buildDir}/platypus"
    }
    suite.dependsOn netbeans
}

task netBeansRun(type: Exec) {
    def win = System.getProperty('os.name').toLowerCase().contains('windows')
    def executable = win ? 'netbeans.exe' : 'netbeans'
    def separator = win ? ';' : ':'
    doFirst {
        def confFile = project.file("${project.buildDir}${File.separator}testuserdir${File.separator}etc${File.separator}netbeans.conf")
        confFile.parentFile.mkdirs()
        confFile.write "netbeans_extraclusters=\"${project.buildDir}${File.separator}platypus\""
    }
    workingDir "${project.buildDir}"
    if (project.hasProperty('netBeansInstallDir')) {
        executable = "${netBeansInstallDir}${File.separator}bin${File.separator}${executable}"
    } else {
        logger.warn('netBeansInstallDir property is not specified. Assuming \'netbeans\' executable is on PATH.')
    }
    def userDir = "\"${project.buildDir}${File.separator}testuserdir\""
    commandLine executable, '--userdir', userDir, "-J-agentlib:jdwp=transport=dt_socket,suspend=n,server=y,address=${debugPort}"
}
